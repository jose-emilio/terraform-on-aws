# En LocalStack los recursos no persisten entre reinicios del contenedor,
# por lo que el bucket de estado se crea aquí (a diferencia de AWS real,
# donde el bucket fue creado y persiste desde el Lab02).
# El nombre "terraform-state-labs" coincide con el del Lab02 para mantener coherencia.

resource "aws_s3_bucket" "state" {
  bucket = var.bucket_name

  tags = {
    ManagedBy = "terraform"
    Purpose   = "terraform-remote-state"
  }
}

resource "aws_s3_bucket_versioning" "state" {
  bucket = aws_s3_bucket.state.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_public_access_block" "state" {
  bucket = aws_s3_bucket.state.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_dynamodb_table" "lock" {
  name         = var.table_name
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }

  tags = {
    ManagedBy = "terraform"
    Purpose   = "terraform-state-lock"
  }
}
